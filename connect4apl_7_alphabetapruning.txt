:namespace vg
    i←↑{(∧/¨{⍵∊⍳6 7}¨⍵)/⍵},(⊂¨,⍳6 7)∘.+↓(¯1 0 1 1,¨1 1 1 0)∘.×¯1+⍳4
    vm←{(0=⍵[6;])/⍳7} ⋄ add←{b s c←⍵ ⋄ b[b[;c]⍳0;c]←s ⋄ b} ⋄ pp←{'A2'⎕fmt'.OX'[1+⊖⍵]}    
    pnt←{b s←⍵ ⋄ t o←+/¨s(3-s)=⊂b[i] ⋄ 4∊t:1000 ⋄ +/(0=t×o)/t-o}
      val←{b s c d x y←⍵ ⋄ (d=0)∨1000=p←pnt(b←add b s c)s:p+d
          -{m l←⍵ ⋄ l≡⍬:m ⋄ v←val b(3-s)(⊃l)(d-1)(-y)(-m)
              (v>m)∧v≥y:v ⋄ ∇(m⌈v)(1↓l)}x(vm b)}
    ∇ main
      ⍝b←3↓[2]6 10⍴1 1 2 2 ⋄ b[6;⍳7]←0
      b←6 7⍴0
      b←{⎕←pp(b←⍵) ⋄ 0=m←+/,b=0:⍵ ⋄ s←1+2|m
          0=c←{⍵=2:⍎⍞ ⋄ ⍞←v←{val b s ⍵(5⌊m-1)¯10000 10000}¨c←vm b
              c[⊃⍒v]}s:⍵ ⋄ add b s c}⍣≡b
    ∇
:endnamespace
